# Expans√£o de itens (nav tree) #
 
![Static Badge](https://img.shields.io/badge/development-abap-blue)
![GitHub commit activity (branch)](https://img.shields.io/github/commit-activity/t/edmilson-nascimento/Nav-Tree)
![Static Badge](https://img.shields.io/badge/murilo-abap-green)


> üóò Este documento, assim como o neg√≥cio, est√° em constante fase de melhoria e adapta√ß√£o.

N√£o encontrei um nome mais simples para ser t√≠tulo do projeto, logo, eu informei como ST12 ~porque o projeto √© meu e eu coloco o nome mais facil de ser encontrado por mim depois~ por ser um ALV semelhante ao exibido pela transa√ß√£o ST12. Desconsiderar algum problema de indenta√ß√£o. Eu tenho um s√©rio problema de limita√ß√£o do `case`. Como ABAP n√£o √© uma linguagem `case sensitive`, ~~√© muita perca de tempo querer colocar tamanho de fonte em `upper case`~~ eu n√£o vejo necessidade de alterar o `case` sendo que o editor j√° separa com cores diferentes as palavras reservadas. Mas ~~o desenvolvedor n√£o consegue ver as cores diferentes e ainda quer colocacr em casa diferente~~ os outros ABAPer's que compartilham o mesmo usu√°rio pensam diferente de mim, ent√£o eu fa√ßo sem `Pretty Printer`.

Neste exemplo eu tenho uma mostra de como seria se dentro de um relat√≥rio eu pudesse expandir os Itens (ou qualquer outra informa√ß√£o que seja) de forma que fique como um _subtotal ao contr√°rio_. Como padr√£o na maioria dos meus desenvolvimentos, foi usado a classe `CL_GUI_ALV_GRID`. Alguns dados de Companhias A√©reas ser√£o exibidos e ap√≥s expandir ser√£o mostrados os v√¥os referente a cia a√©rea. As aplica√ß√µes s√£o ilimitadas, basta **saber adequar seu cen√°rio com a tecnologia que melhor atende**.

## Informa√ß√µes Exibidas ##
Uma tela de sele√ß√£o com as principais chaves √© exibida.

![N|Solid](https://uploaddeimagens.com.br/images/001/289/531/original/tela-de-selecao.png)

Ap√≥s, √© exibido o relat√≥rio com um `hotspot` que simboliza uma `navega√ß√£o tree`, conforme imagem.

![N|Solid](https://uploaddeimagens.com.br/images/001/289/535/original/click-01.png?1518610599)

A lista √© expandida ao clicar no √≠cone da pasta, fazendo com que mais detalhes sejam exibidos, e no caso, voos da cia a√©rea referente.

![N|Solid](https://uploaddeimagens.com.br/images/001/289/537/original/click-02.png?1518610742)

Essa funcionalidade √© explica nos metodos abaixo.

Alguns m√©todos est√£o sem desenvolvimento, mas eu preferi manter para poss√≠veis melhorias. Enquanto eu estiver com acesso ao SAP nessa vers√£o, vou continuar trabalhando com classes locais e no caso dessa √© a `LCL_REPORT` com os seguintes m√©todos:

* public section
	* [display_data](#display_data)
	* [get_data](#get_data)
	* [initial](#initial)  

* protected section
	* [on_added_function](#on_added_function)
	* [on_link_click](#on_link_click)

* private section
	* [add_items](#add_items)
	* [change](#change)
	* [del_items](#del_items)  
	* [organize](#add_items)
	* [process](#process)
	* [search](#del_items)  

### public section ###

#### display_data ####
Tendo o objetivo de exibir as informa√ß√µes, fazer considera√ß√µes finais referente a exibi√ß√£o, como ordena√ß√£o, hotspot e outros. Normalmente eu fa√ßo a chamada de sub-rotinas internas em m√©todos privados, mas no caso, preferi manter tudo dentro desse mesmo metodo.
```abap
  method display_data .

    data:
      events  type ref to cl_salv_events_table,
      display type ref to cl_salv_display_settings,
      sorts   type ref to cl_salv_sorts,
      column  type ref to cl_salv_column_list,
      columns type ref to cl_salv_columns_table.

    check lines( me->out ) gt 0 .


    try.
        call method cl_salv_table=>factory
          importing
            r_salv_table = me->salv_table
          changing
            t_table      = me->out .


        events = me->salv_table->get_event( ).

        set handler me->on_link_click for events.
        set handler me->on_added_function for events.

        me->salv_table->set_screen_status(
          pfstatus      = 'STANDARD_FULLSCREEN'
          report        = 'SAPLKKBL'
          set_functions = me->salv_table->c_functions_all ).


        columns = me->salv_table->get_columns( ).

        columns->set_optimize( 'X' ).
        column ?= columns->get_column( 'NAVTREE' ).
        column->set_icon( if_salv_c_bool_sap=>true ).
        column->set_cell_type( if_salv_c_cell_type=>hotspot ).
        column->set_long_text( 'N√≠vel' ).
        column->set_symbol( if_salv_c_bool_sap=>true ).

*       Layout de Zebra
        display = me->salv_table->get_display_settings( ) .
        display->set_striped_pattern( cl_salv_display_settings=>true ) .

*       Ordena√ß√£o de campos
        sorts = me->salv_table->get_sorts( ) .
        sorts->add_sort('CARRID') .

        me->salv_table->display( ).

      catch cx_salv_msg .
      catch cx_salv_not_found .
      catch cx_salv_existing .
      catch cx_salv_data_error .
      catch cx_salv_object_not_found .

    endtry.

  endmethod .                    "generate_output
```
#### get_data ####
Como uma arquitetura padr√£o que uso para recupera√ß√£o de informa√ß√µes, esse m√©todo por sua vez faz chamada de outros dois que s√£o respons√°veis de buscar e organizar as informa√ß√µes.
```abap
  method get_data.

    me->search(
      exporting
        carrid  = carrid
        connid  = connid
        fldate  = fldate
      changing
        scarr   = scarr
        sflight = sflight
    ).

    me->organize(
      exporting
        scarr   = scarr
        sflight = sflight
    ).

  endmethod.                    "GET_DATA
```
#### initial ####
Em outros report's eu uso este para habilitar/desabilitar e preencher campos da tela de sele√ß√£o. Preferi manter para que eu posso fazer melhorias em vers√µes posteriores.
```abap
  method initial .
  endmethod .                    "initial
```

### protected section ###

#### on_added_function ####
Neste temos a chamada do outro m√©todo, process, que ser√° referente a a√ß√µes requisitas ap√≥s a gera√ß√£o do relat√≥rio.
```abap
  method on_added_function .

    me->process( ) .

  endmethod .                    "on_added_function
```

#### on_link_click ####
A a√ß√£o do hotspot, ser√° contemplada nesse m√©todo, de forma a controlar de acordo com a coluna onde foi iniciada a a√ß√£o.
```abap
  method on_link_click .

    data:
      columns type ref to cl_salv_columns_table .

    case column .

      when 'NAVTREE' .

        me->change(
          exporting
            row = row
        ).

        columns = me->salv_table->get_columns( ).
        columns->set_optimize( if_salv_c_bool_sap=>true ).
        me->salv_table->refresh( ) .

      when others .

    endcase .

  endmethod .
```
### private section ###

#### add_items ####
Ao clicar no `hotspot`, novos itens s√£o adicionados, o que passa ao usu√°rio a impress√£o de um nova lista expandida. Isso √© feito atrav√©s do m√©todo abaixo.
```abap
  method add_items .

    data:
      index    type syindex,
      line     type sflight,
      out_line type me->ty_out,
      new_line type ref to data .

    field-symbols:
      <line> type me->ty_out .

    read table me->out assigning <line> index row .
    if sy-subrc eq 0 .
      <line>-navtree = '5' .
      unassign <line> .
    endif .

    index = row .

    create data new_line like line of me->out .
    assign new_line->* to <line> .

    loop at me->sflight into line
                  where carrid eq carrid .

      index = index + 1 .

      <line>-navtree   = '3' .
      <line>-carrid    = line-carrid .

      read table me->out into out_line
        with key carrid = carrid .
      if sy-subrc eq 0 .
        <line>-carrname  = out_line-carrname .
        <line>-currcode  = out_line-currcode .
      endif .

      <line>-connid    = line-connid .
      <line>-fldate    = line-fldate .
      <line>-planetype = line-planetype .

      insert <line> into me->out index index .

    endloop .

  endmethod.
```

#### change ####
Esse m√©todo, det√©m a chamada de outros dois m√©todos, de acordo com o √≠cone que foi clicado, pode ser para expandir ou recolher uma lista. Isso √© definido na rotina abaixo.
```abap
  method change .

    field-symbols:
      <line> type me->ty_out .

    read table me->out assigning <line> index row .
    if sy-subrc eq 0 .

      case <line>-navtree .
        when '4' . "Pasta fechada

          me->add_items(
            exporting
              row    = row
              carrid = <line>-carrid
          ).

        when '5' . "Pasta aberta

          me->del_items(
            exporting
              row  = row
              carrid = <line>-carrid
          ).

      endcase .

    endif .

  endmethod .
```

#### del_items ####
Ao clicar no √≠cone, quando ja foi expandido, o usu√°rio pode tamb√©m recolher essa lista, e isso √© contemplado na rotina a seguir.
```abap
  method del_items .

    data:
      index type syindex,
      line  type sflight .

    field-symbols:
      <line> type me->ty_out .

    read table me->out assigning <line> index row .

    if sy-subrc eq 0 .

      <line>-navtree = '4' .

      delete me->out where navtree eq '3'
                       and carrid  eq carrid .

    endif .

  endmethod .

```

#### organize ####
A mostra de informa√ß√µes √© feita em dois passos: recupera√ß√£o de dados e organiza√ß√£o desses dados para que se tornem informa√ß√µes. Essa segunda parte √© contemplada neste m√©todo.
```abap
  method organize .

    data:
      scarr_line type scarr,
      out_line   type me->ty_out .

    refresh:
      me->out .

    loop at scarr into scarr_line .

      out_line-navtree  = '4' .
      out_line-carrid   = scarr_line-carrid .
      out_line-carrname = scarr_line-carrname .
      out_line-currcode = scarr_line-currcode .

      append out_line to me->out .
      clear  out_line .

    endloop .

  endmethod .
```

#### process ####
Este m√©todo contempla a√ß√µes que s√£o executadas apos a gera√ß√£o do relat√≥rio, como um `refresh` por exemplo. Para essa solu√ß√£o, **ainda** n√£o foi implementada nenhuma a√ß√£o, apenas uma atualiza√ß√£o mas n√£o esta sendo chamada.
```abap
  method process .

    case sy-ucomm .

      when 'REFRESH' .

        if me->salv_table is bound .

          me->salv_table->refresh( ) .

        endif .

      when others .

    endcase .

  endmethod .                    "process
```

#### search ####
Este √© respons√°vel pelo acesso as tabelas e busca das informa√ß√µes no banco de dados, de forma e recuperar esses dados de acordo com os filtros da tela de sele√ß√£o.
```abap
  method search .

    data:
      filter type bvw_tab_where .

    refresh:
      scarr, sflight .

    if lines( carrid ) eq 0 .
    else .
      append 'carrid in carrid' to filter .
    endif .


    if lines( filter ) eq 0 .

      select *
        into table scarr
        from scarr .
    else .

      select *
        into table scarr
        from scarr
       where (filter) .
     endif .

    if sy-subrc eq 0 .

      refresh:
        filter .

      append 'carrid eq scarr-carrid' to filter .

      if lines( connid ) eq 0 .
      else .
        append 'and connid in connid' to filter .
      endif .


      if lines( fldate ) eq 0 .
      else .
        append 'and fldate in fldate' to filter .
      endif .

      select *
        into table sflight
        from sflight
         for all entries in scarr
       where (filter) .

    endif .

    free:
      filter .

  endmethod.
```
